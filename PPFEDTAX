**************************************************************/   28732379
*  PROGRAM: PPFEDTAX                                         */   28732379
*  RELEASE: ___2379______ SERVICE REQUEST(S): ___102873____  */   28732379
*  NAME:______TAYAG______ MODIFICATION DATE:  ___01/08/20__  */   28732379
*  DESCRIPTION:                                              */   28732379
*  CHANGE FEDERAL WITHHOLDING TAX CALCULATION BASED ON       */   28732379
*  2020 IRS PUB 15 T.                                        */   28732379
**************************************************************/   28732379
**************************************************************/   28311915
*  PROGRAM: PPFEDTAX                                         */   28311915
*  RELEASE: ___1915______ SERVICE REQUEST(S): ____82831____  */   28311915
*  NAME:_BASKAR CHITRAVEL MODIFICATION DATE:  ___03/30/10__  */   28311915
*  DESCRIPTION:                                              */   28311915
*  CALCULATE AND ADD THE ADDITIONAL TAX FOR NRAS USING INPUT */   28311915
*  NRA RATE SET LOADED INTO CPWSXFTA'S FEDTAX TABLE ARRAY.   */   28311915
**************************************************************/   28311915
**************************************************************/   15611721
*  PROGRAM: PPFEDTAX                                         */   15611721
*  RELEASE: ___1721______ SERVICE REQUEST(S): ____81561____  */   15611721
*  NAME:_____RGLASER_____ MODIFICATION DATE:  ___07/05/06__  */   15611721
*  DESCRIPTION:                                              */   15611721
*  ADD LOGIC TO INCREASE NRA EMPLOYEE'S GROSS AMT BEFORE THE */   15611721
*  COMPUTATION OF FEDERAL WITHHOLDING TAX.  THE AMOUNT THAT  */   15611721
*  IS ADDED COMES FROM 1 OF 3 NEW SYSTEM PARMS AND DEPENDS   */   15611721
*  ON THE EMPLOYEE'S PAYROLL SCHEDULE.  THIS CHANGE IS       */   15611721
*  REQUIRED IN ORDER TO REMAIN IN COMPLAINCE WITH IRS        */   15611721
*  REGULATIONS IN RESPECT TO THE AMOUNT WITHHELD FOR NRA     */   15611721
*  EMPLOYEES. THE INCREASED GROSS AMT IS NOT CARRIED FURTHER.*/   15611721
*  THE INCREASED GROSS AMT IS NOT CARRIED FOR FURTHER        */   15611721
*  PROCESSING.                                               */   15611721
**************************************************************/   15611721
**************************************************************/   69761691
*  PROGRAM: PPFEDTAX                                         */   69761691
*  RELEASE: ___1691______ SERVICE REQUEST(S): __16976,16963  */   69761691
*  NAME:________JLT______ MODIFICATION DATE:  ___01/12/06__  */   69761691
*  DESCRIPTION:                                              */   69761691
*  -TREAT CITIZEN CODE "A" IN THE SAME MANNER AS 'N' IN MOST */   69761691
*   CASES.                                                   */   69761691
*  -REMOVE THE BACKING OUT OF FEL GROSS FROM PRIMARY FWT GRS.*/   69761691
*   (I.E., THE 'FEL' DOS FWT SUBJECT GRS IND IS NOW SET OFF).*/   69761691
*  - GENERATE A MESSAGE IF THE FWT EXCEEDS $99,999.99.       */   69761691
**************************************************************/   69761691
**************************************************************/   69331612
*  PROGRAM: PPFEDTAX                                         */   69331612
*  RELEASE: ___1612______ SERVICE REQUEST(S): ___16933_____  */   69331612
*  NAME:________JLT______ MODIFICATION DATE:  ___10/20/04__  */   69331612
*  DESCRIPTION:                                              */   69331612
*  - ADD PROCESSING FOR FELLOWSHIP GROSS (POSTDOC).          */   69331612
**************************************************************/   69331612
**************************************************************/   48861503
*  PROGRAM:  PPFEDTAX                                        */   48861503
*  RELEASE: ___1503______ SERVICE REQUEST(S): ____14886____  */   48861503
*  NAME:___S.ISAACS______ MODIFICATION DATE:  ___07/23/03__  */   48861503
*   DESCRIPTION:                                             */   48861503
*     ADDED CAPABILITY TO HANDLE FED FLAT TAX ON BONUSES     */   48861503
*      IF REQUIRED.                                          */   48861503
**************************************************************/   36100633
*  PROGRAM:  PPFEDTAX                                        */   36100633
*  RELEASE # ___0633___   SERVICE REQUEST NO(S)____3610______*/   36100633
*  NAME __SRS__________   MODIFICATION DATE ____07/24/91_____*/   36100633
*  DESCRIPTION                                               */   36100633
*                                                            */   36100633
*  THIS IS A NEW PROGRAM FOR ORCA                            */   36100633
*                                                            */   36100633
**************************************************************/   36100633
 IDENTIFICATION DIVISION.                                         PPFEDTAX
 PROGRAM-ID. PPFEDTAX.                                            PPFEDTAX
 INSTALLATION. UNIVERSITY OF CALIFORNIA.                          PPFEDTAX
 AUTHOR.                                                          PPFEDTAX
 DATE-WRITTEN.                                                    PPFEDTAX
 DATE-COMPILED.                                                   PPFEDTAX
*REMARKS.                                                         PPFEDTAX
******************************************************************PPFEDTAX
*                                                                *PPFEDTAX
*  THIS MODULE COMPUTES FEDERAL TAXES.                           *PPFEDTAX
*                                                                *PPFEDTAX
******************************************************************PPFEDTAX
 ENVIRONMENT DIVISION.                                            PPFEDTAX
     SKIP3                                                        PPFEDTAX
******************************************************************PPFEDTAX
*                                                                *PPFEDTAX
*                         DATA DIVISION                          *PPFEDTAX
*                                                                *PPFEDTAX
******************************************************************PPFEDTAX
 DATA DIVISION.                                                   PPFEDTAX
     SKIP1                                                        PPFEDTAX
******************************************************************PPFEDTAX
*                                                                *PPFEDTAX
*                    WORKING-STORAGE SECTION                     *PPFEDTAX
*                                                                *PPFEDTAX
******************************************************************PPFEDTAX
 WORKING-STORAGE SECTION.                                         PPFEDTAX
     SKIP1                                                        PPFEDTAX
 77  S4                           PIC S9(4)  COMP.                PPFEDTAX
 77  S5                           PIC S9(4)  COMP.                PPFEDTAX
 77  S6                           PIC S9(4)  COMP.                PPFEDTAX
 77  M40020                       PIC 9(05)  VALUE 40020.         PPFEDTAX
 77  M40030                       PIC 9(05)  VALUE 40030.         28732379
 77  M40090                       PIC 9(05)  VALUE 40090.         69761691
 77  WK-TOT-FWT-ADJ-GRS           PIC S9(7)V99  COMP-3.           69331612
 77  WK-ADJ-ANNUAL-WAGE-AMT       PIC S9(7)V99  COMP-3.           28732379
 77  WK-ANNUAL-BASE               PIC S9(7)V99  COMP-3.           28732379
 77  WK-1G-AMOUNT                 PIC S9(7)V99  COMP-3.           28732379
 77  WK-1G-MARRIED-AMT            PIC S9(7)V99  COMP-3            28732379
                                             VALUE 12900.         28732379
 77  WK-1G-OTHERWISE-AMT          PIC S9(7)V99  COMP-3            28732379
                                             VALUE 8600.          28732379
 77  WK-1H-AMOUNT                 PIC S9(7)V99  COMP-3.           28732379
 77  WK-1I-AMOUNT                 PIC S9(7)V99  COMP-3.           28732379
 77  WK-1J-AMOUNT                 PIC S9(7)V99  COMP-3.           28732379
 77  WK-1K-AMOUNT                 PIC S9(7)V99  COMP-3.           28732379
 77  WK-3A-AMOUNT                 PIC S9(7)V99  COMP-3.           28732379
 77  WK-PERSONAL-ALLOWANCE        PIC S9(7)V99  COMP-3            28732379
                                             VALUE 4300.          28732379
 77  WK-DISPLAY-AMT               PIC -9(7).99  .                 28732379
 77  WS-DED-TO-TAKE               PIC S9(7)V99  COMP-3.           69761691
 77  WS-NUMBER-OF-PAY-PERIODS     PIC S9(7)V99  COMP-3.           28732379
     EJECT                                                        PPFEDTAX
******************************************************************PPFEDTAX
*                                                                *PPFEDTAX
*                    LINKAGE SECTION                             *PPFEDTAX
*                                                                *PPFEDTAX
******************************************************************PPFEDTAX
 LINKAGE SECTION.                                                 PPFEDTAX
 01  FEDERAL-TAX-AREA.                                            PPFEDTAX
                                  COPY CPWSXFTA.                  PPFEDTAX
     EJECT                                                        PPFEDTAX
 01  FEDERAL-TAX-INTERFACE.                                       PPFEDTAX
                                  COPY CPLNKFED.                  PPFEDTAX
     EJECT                                                        PPFEDTAX
******************************************************************PPFEDTAX
*                                                                *PPFEDTAX
*                       PROCEDURE DIVISION                       *PPFEDTAX
*                                                                *PPFEDTAX
******************************************************************PPFEDTAX
     SKIP2                                                        PPFEDTAX
 PROCEDURE DIVISION       USING FEDERAL-TAX-AREA                  PPFEDTAX
                                FEDERAL-TAX-INTERFACE.            PPFEDTAX
     SKIP2                                                        PPFEDTAX
 0100-FEDERAL-TAX-ROUTINE  SECTION.                               69331612
 0100-FEDERAL-TAX-DEDUCTION.                                      PPFEDTAX
     SKIP1                                                        PPFEDTAX
     MOVE ZERO     TO WK-ADJ-ANNUAL-WAGE-AMT                      28732379
                      WK-ANNUAL-BASE                              28732379
                      WK-1G-AMOUNT                                28732379
                      WK-1H-AMOUNT                                28732379
                      WK-1I-AMOUNT                                28732379
                      WK-1J-AMOUNT                                28732379
                      WK-1K-AMOUNT                                28732379
                      WK-3A-AMOUNT                                28732379
                      WK-DISPLAY-AMT.                             28732379
     DISPLAY '    FEDTAX CALLED.'.                                D
     MOVE  ZERO    TO KFED-FEL-FWT-DED-TO-TAKE.                   69331612
     IF      KFED-POSTDOC-FEL-NRA-GRS  > ZERO                     69331612
             DISPLAY '      PD FEL NRA GRS...'                    D
             PERFORM  1000-POSTDOC-FEL-NRA-DED                    69331612
     END-IF.                                                      69331612
     SKIP1                                                        15611721
     IF      KFED-CT-CODE-NON-RESID-ALIEN-T                       15611721
     OR      KFED-CT-CODE-NONR-ALN-SPECCTRY                       15611721
             DISPLAY '      NRA DED PROCESS..'                    D
             PERFORM  1500-NRA-DED-PROCESSING                     15611721
             THRU     1500-EXIT                                   15611721
     END-IF.                                                      15611721
     IF      KFED-GROSS-FWT1  < ZERO                              PPFEDTAX
             DISPLAY '      NEGATIVE KFED-GROSS-FWT1'             D
             COMPUTE KFED-GROSS-FWT2 =                            PPFEDTAX
                     KFED-GROSS-FWT2 + KFED-GROSS-FWT1            PPFEDTAX
             MOVE ZEROS             TO KFED-GROSS-FWT1            PPFEDTAX
     END-IF.                                                      PPFEDTAX
     SKIP1                                                        PPFEDTAX
     IF      KFED-GROSS-FWT2  < ZERO                              PPFEDTAX
             DISPLAY '      NEGATIVE KFED-GROSS-FWT2'             D
             COMPUTE KFED-GROSS-FWT1 =                            PPFEDTAX
                     KFED-GROSS-FWT1 + KFED-GROSS-FWT2            PPFEDTAX
             MOVE ZEROS             TO KFED-GROSS-FWT2            PPFEDTAX
     END-IF.                                                      PPFEDTAX
     SKIP1                                                        48861503
     IF      KFED-GROSS-FWT3  < ZERO                              48861503
             DISPLAY '      NEGATIVE KFED-GROSS-FWT3'             D
             COMPUTE KFED-GROSS-FWT1 =                            48861503
                     KFED-GROSS-FWT1 + KFED-GROSS-FWT3            48861503
             MOVE ZEROS             TO KFED-GROSS-FWT3            48861503
     END-IF.                                                      48861503
     SKIP1                                                        PPFEDTAX
     IF      KFED-GROSS-FWT1 = ZEROS                              PPFEDTAX
             MOVE KFED-REDUCTIONS-FWT TO KFED-AMOUNT              PPFEDTAX
         IF  KFED-REDUCTIONS-FWT < ZERO                           PPFEDTAX
             NEXT SENTENCE                                        PPFEDTAX
         ELSE                                                     PPFEDTAX
             MOVE ZEROS TO KFED-DED-TO-TAKE                       PPFEDTAX
             GO TO 0500-FEDERAL-TAX-GROSS                         PPFEDTAX
         END-IF                                                   PPFEDTAX
     END-IF.                                                      PPFEDTAX
     SKIP1                                                        PPFEDTAX
     IF KFED-TAX-PERIOD = 'B'                                     28732379
         MOVE 26 TO WS-NUMBER-OF-PAY-PERIODS                      28732379
     ELSE                                                         28732379
         MOVE 12 TO WS-NUMBER-OF-PAY-PERIODS                      28732379
     END-IF.                                                      28732379
                                                                  28732379
     PERFORM                                                      PPFEDTAX
        VARYING S4 FROM 1 BY 1                                    PPFEDTAX
*****     UNTIL XFTA-PERIOD (S4) = KFED-TAX-PERIOD                28732379
*****        OR S4 > 4                                            28732379
          UNTIL XFTA-IRS-SCHEDULE (S4)                            28732379
                                 = KFED-IRS-SCHEDULE              28732379
             OR S4 > 2                                            28732379
     END-PERFORM.                                                 PPFEDTAX
     SKIP2                                                        PPFEDTAX
**** IF      S4 > +4                                              28732379
     IF      S4 > +2                                              28732379
             MOVE +1 TO S4                                        PPFEDTAX
****         MOVE M40020 TO KFED-MSSG-NO                          28732379
* NO TAX TABLE TO MATCH EMPLOYEE'S PAY PERIOD, DEFAULTING TO 1ST  28732379
             MOVE M40030 TO KFED-MSSG-NO                          28732379
* NO TAX TABLE TO MATCH EE'S DERIVED SCHEDULE, DEFAULTING TO 1ST  28732379
     END-IF.                                                      PPFEDTAX
     SKIP1                                                        PPFEDTAX
******************************************************************PPFEDTAX
*    IF THE IRS SCHEDULE PASSED   IS NOT MATCHED - DEFAULT TO THE 28732379
*    FIRST TAX TABLE LOADED FOR THIS CYCLE - ISSUE MESSAGE TO     PPFEDTAX
*    USER - AND CONTINUE TO PROCESSING - S4 NOW POINTS            PPFEDTAX
*    TO THE PROPER TAX TABLE - S5 WILL LATER POINT TO THE ITEM    PPFEDTAX
*    IN THE TAX TABLE - MARRIED STARTS AT POSITION 12 - SINGLE    PPFEDTAX
*    POSITION 1. S6 WILL POINT TO END OF TABLE (S=11, M=22).      PPFEDTAX
******************************************************************PPFEDTAX
     SKIP1                                                        PPFEDTAX
     IF      KFED-GROSS-FWT1 > ZEROS                              PPFEDTAX
         OR  KFED-AMOUNT  <  ZEROES                               PPFEDTAX
             COMPUTE KFED-BASE = KFED-GROSS-FWT1 -                PPFEDTAX
                                        KFED-REDUCTIONS-FWT       PPFEDTAX
             COMPUTE WK-ANNUAL-BASE = KFED-BASE *                 28732379
                                        WS-NUMBER-OF-PAY-PERIODS  28732379
                                                                  D
             IF  KFED-W4-2020-FORM = 1                            D
                 DISPLAY '2020 W4 FORM'                           D
             ELSE                                                 D
                 DISPLAY '2019 W4 FORM'                           D
             END-IF                                               D
             DISPLAY 'KFED-IRS-SCHEDULE   :' KFED-IRS-SCHEDULE    D
             MOVE KFED-GROSS-FWT1 TO WK-DISPLAY-AMT               D
             DISPLAY 'KFED-GROSS-FWT1     :' WK-DISPLAY-AMT       D
             MOVE KFED-REDUCTIONS-FWT TO WK-DISPLAY-AMT           D
             DISPLAY 'KFED-REDUCTIONS-FWT :' WK-DISPLAY-AMT       D
             MOVE KFED-BASE       TO WK-DISPLAY-AMT               D
             DISPLAY 'KFED-BASE           :' WK-DISPLAY-AMT       D
             DISPLAY 'KFED-FEDTAX-MARITAL :' KFED-FEDTAX-MARITAL  D
             MOVE KFED-FEDTAX-EXEMPT TO WK-DISPLAY-AMT            D
             DISPLAY 'KFED-FEDTAX-EXEMPT  :' WK-DISPLAY-AMT       D
                                                                  28732379
             IF      KFED-BASE < ZEROS                            PPFEDTAX
             DISPLAY '      NEGATIVE KFED-BASE      '             D
                     COMPUTE KFED-AMOUNT = - KFED-BASE            PPFEDTAX
                     MOVE ZEROS TO KFED-BASE                      PPFEDTAX
             ELSE                                                 PPFEDTAX
                     MOVE ZEROS TO KFED-AMOUNT                    PPFEDTAX
             END-IF                                               PPFEDTAX
     ELSE                                                         PPFEDTAX
             MOVE KFED-REDUCTIONS-FWT TO KFED-AMOUNT              PPFEDTAX
             MOVE ZEROS TO KFED-BASE                              PPFEDTAX
     END-IF.                                                      PPFEDTAX
     SKIP1                                                        PPFEDTAX
**** ADD NEW SET OF RATES OF HEAD OF HOUSEHOLD WHICH ARE TO BE    28732379
**** USED ONLY IF EMPLOYEE HAS W4 2020 FORM.                      28732379
     IF      KFED-FEDTAX-MARITAL = 'H'                            28732379
     AND     KFED-W4-2020-FORM = 1                                28732379
             MOVE 23 TO S5                                        28732379
             COMPUTE S6 = XFTA-NO-HEADOFH (S4) + 23               28732379
     ELSE                                                         28732379
     IF      KFED-FEDTAX-MARITAL = 'M'                            PPFEDTAX
**** NRAS WILL USE SAME SET OF TABLES SO COMMENTING OUT CONDITION.28732379
**** AND NOT KFED-CT-CODE-NON-RESID-ALIEN-T                       28732379
**** AND NOT KFED-CT-CODE-NONR-ALN-SPECCTRY                       28732379
             MOVE 12 TO S5                                        PPFEDTAX
             COMPUTE S6 = XFTA-NO-MARRIED (S4) + 12               PPFEDTAX
     ELSE                                                         PPFEDTAX
             MOVE  1 TO S5                                        PPFEDTAX
             COMPUTE S6 = XFTA-NO-SINGLES (S4) + 1                PPFEDTAX
     END-IF                                                       28732379
     END-IF.                                                      PPFEDTAX
     SKIP1                                                        PPFEDTAX
******************************************************************15611721
* THIS IS NOT APPLICABLE ANYMORE SO COMMENTING OUT THE CODE       28732379
* AND REMOVING THE PREVIOUS COMMENTS.                             28732379
*                                                                 28732379
*                                                                 28732379
*                                                                 28732379
*                                                                 28732379
******************************************************************15611721
**** IF      KFED-CT-CODE-NON-RESID-ALIEN-T                       28732379
****         COMPUTE KFED-BASE = KFED-BASE - XFTA-EXEMPTION (S4)  28732379
**** ELSE                                                         28732379
****         COMPUTE KFED-BASE = KFED-BASE                        28732379
****                - (XFTA-EXEMPTION (S4) * KFED-FEDTAX-EXEMPT)  28732379
**** END-IF.                                                      28732379
                                                                  28732379
**** EMPLOYEE HAS 2020 W4 FORM                                    28732379
     IF      KFED-W4-2020-FORM = 1                                28732379
             COMPUTE WK-ANNUAL-BASE = WK-ANNUAL-BASE +            28732379
                                        KFED-W4-STEP4A-AMT        28732379
             IF  KFED-W4-STEP2C = 1                               28732379
                 MOVE 0     TO WK-1G-AMOUNT                       28732379
             ELSE                                                 28732379
                 IF  KFED-FEDTAX-MARITAL = 'M'                    28732379
                     MOVE WK-1G-MARRIED-AMT                       28732379
                                TO WK-1G-AMOUNT                   28732379
                 ELSE                                             28732379
                     MOVE WK-1G-OTHERWISE-AMT                     28732379
                                TO WK-1G-AMOUNT                   28732379
                 END-IF                                           28732379
             END-IF                                               28732379
                                                                  28732379
             COMPUTE WK-1H-AMOUNT  = KFED-W4-STEP4B-AMT +         28732379
                                        WK-1G-AMOUNT              28732379
                                                                  28732379
             COMPUTE WK-ADJ-ANNUAL-WAGE-AMT =                     28732379
                                     WK-ANNUAL-BASE -             28732379
                                        WK-1H-AMOUNT              28732379
             IF  WK-ADJ-ANNUAL-WAGE-AMT <= 0                      28732379
                 MOVE 0 TO WK-ADJ-ANNUAL-WAGE-AMT                 28732379
             END-IF                                               28732379
     ELSE                                                         28732379
**** EMPLOYEE HAS 2019 OR EARLIER W4 FORM                         28732379
             COMPUTE WK-1K-AMOUNT  = KFED-FEDTAX-EXEMPT *         28732379
                                        WK-PERSONAL-ALLOWANCE     28732379
             COMPUTE WK-ADJ-ANNUAL-WAGE-AMT =                     28732379
                                     WK-ANNUAL-BASE -             28732379
                                        WK-1K-AMOUNT              28732379
             IF  WK-ADJ-ANNUAL-WAGE-AMT <= 0                      28732379
                 MOVE 0 TO WK-ADJ-ANNUAL-WAGE-AMT                 28732379
             END-IF                                               28732379
     END-IF.                                                      28732379
                                                                  28732379
     SKIP1                                                        PPFEDTAX
     IF      KFED-BASE < ZEROS                                    PPFEDTAX
             MOVE ZEROS TO KFED-BASE                              PPFEDTAX
     END-IF.                                                      PPFEDTAX
     IF      KFED-FDTX-EXEMPT-EXMPT-FED-TX                        PPFEDTAX
       OR   (KFED-FDTX-EXEMPT-W4E-ON-FILE AND                     PPFEDTAX
             NOT KFED-CT-CODE-NONR-ALN-SPECCTRY AND               69760691
             NOT KFED-CT-CODE-NON-RESID-ALIEN-T)                  PPFEDTAX
             MOVE ZEROS TO KFED-DED-TO-TAKE                       PPFEDTAX
     ELSE                                                         PPFEDTAX
*****        IF   KFED-BASE NOT > XFTA-SALARY (S4, S5)            28732379
             IF   WK-ADJ-ANNUAL-WAGE-AMT                          28732379
                            NOT > XFTA-SALARY (S4, S5)            28732379
                  MOVE ZEROS TO KFED-DED-TO-TAKE                  28732379
                                WS-DED-TO-TAKE                    28732379
*****             PERFORM 2000-CALC-NRA-DEDUCTION THRU 2000-EXIT  28732379
                  MOVE WS-DED-TO-TAKE TO KFED-DED-TO-TAKE         28732379
             ELSE                                                 28732379
                  PERFORM                                         PPFEDTAX
                    VARYING S5 FROM S5 BY +1                      PPFEDTAX
                      UNTIL S5 = S6                               PPFEDTAX
*****                    OR XFTA-SALARY (S4, S5) > KFED-BASE      28732379
                         OR XFTA-SALARY (S4, S5) >                28732379
                                      WK-ADJ-ANNUAL-WAGE-AMT      28732379
                  END-PERFORM                                     PPFEDTAX
                  ADD -1 TO S5                                    PPFEDTAX
*****             COMPUTE KFED-DED-TO-TAKE ROUNDED =              69761691
                  COMPUTE WS-DED-TO-TAKE  ROUNDED =               69761691
*****             XFTA-TAX (S4, S5) + ((KFED-BASE -               28732379
                  XFTA-TAX (S4, S5) + ((WK-ADJ-ANNUAL-WAGE-AMT -  28732379
                  XFTA-SALARY (S4, S5)) * XFTA-PCT (S4, S5))      PPFEDTAX
*****             PERFORM 2000-CALC-NRA-DEDUCTION THRU 2000-EXIT  28732379
                  COMPUTE WS-DED-TO-TAKE  ROUNDED =               28732379
                       WS-DED-TO-TAKE / WS-NUMBER-OF-PAY-PERIODS  28732379
                                                                  28732379
*****             TAX CREDITS                                     28732379
                  IF  KFED-W4-2020-FORM = 1                       28732379
                      MOVE KFED-W4-STEP3-AMT TO WK-3A-AMOUNT      28732379
                  ELSE                                            28732379
                      MOVE 0                 TO WK-3A-AMOUNT      28732379
                  END-IF                                          28732379
                                                                  28732379
                  COMPUTE WK-3A-AMOUNT = WK-3A-AMOUNT /           28732379
                                         WS-NUMBER-OF-PAY-PERIODS 28732379
                                                                  28732379
                  COMPUTE WS-DED-TO-TAKE  ROUNDED =               28732379
                       WS-DED-TO-TAKE - WK-3A-AMOUNT              28732379
                                                                  28732379
                  IF  WS-DED-TO-TAKE < 0                          28732379
                      MOVE 0 TO WS-DED-TO-TAKE                    28732379
                  END-IF                                          28732379
                                                                  28732379
                  MOVE WS-DED-TO-TAKE  TO WK-DISPLAY-AMT          D
                  DISPLAY 'WS-DED-TO-TAKE      :' WK-DISPLAY-AMT  D
                                                                  28732379
                  MOVE WS-DED-TO-TAKE TO KFED-DED-TO-TAKE         69761691
                  IF WS-DED-TO-TAKE NOT = KFED-DED-TO-TAKE        69761691
                      MOVE M40090 TO KFED-MSSG-NO                 69761691
                  END-IF                                          69761691
             END-IF                                               28732379
     END-IF.                                                      PPFEDTAX
     SKIP2                                                        PPFEDTAX
     SKIP2                                                        15611721
 0500-FEDERAL-TAX-GROSS.                                          PPFEDTAX
     SKIP1                                                        PPFEDTAX
***  IF      KFED-GROSS-FWT2 > ZEROS                              48861503
***          COMPUTE KFED-BASE = KFED-GROSS-FWT2 - KFED-AMOUNT    48861503
***          IF      KFED-BASE < ZEROS                            48861503
***                  MOVE ZEROS TO KFED-BASE                      48861503
***          ELSE    NEXT SENTENCE                                48861503
***          END-IF                                               48861503
***  ELSE                                                         48861503
***          MOVE ZEROS TO KFED-BASE                              48861503
***  END-IF.                                                      48861503
***  SKIP1                                                        48861503
***  MOVE ZEROS TO KFED-AMOUNT.                                   48861503
***  COMPUTE KFED-WRKPCT = KFED-FLAT-FWT-PERCENT / 100.           48861503
***  COMPUTE KFED-DED-TO-TAKE ROUNDED =  KFED-DED-TO-TAKE +       48861503
***                                     (KFED-BASE * KFED-WRKPCT).48861503
***  SKIP1                                                        48861503
     SKIP1                                                        48861503
     IF      KFED-GROSS-FWT2 > ZEROS                              48861503
       IF KFED-AMOUNT >= KFED-GROSS-FWT2                          48861503
             COMPUTE KFED-AMOUNT = KFED-AMOUNT - KFED-GROSS-FWT2  48861503
             MOVE ZEROS TO KFED-BASE                              48861503
       ELSE                                                       48861503
             COMPUTE KFED-BASE = KFED-GROSS-FWT2 - KFED-AMOUNT    48861503
             MOVE ZEROS TO KFED-AMOUNT                            48861503
            COMPUTE KFED-WRKPCT = KFED-FLAT-FWT-PERCENT / 100     48861503
*****       COMPUTE KFED-DED-TO-TAKE ROUNDED = KFED-DED-TO-TAKE + 69761691
*****                                   (KFED-BASE * KFED-WRKPCT) 69761691
            COMPUTE WS-DED-TO-TAKE  ROUNDED =                     69761691
              KFED-DED-TO-TAKE + (KFED-BASE * KFED-WRKPCT)        69761691
            MOVE WS-DED-TO-TAKE TO KFED-DED-TO-TAKE               69761691
            IF WS-DED-TO-TAKE NOT = KFED-DED-TO-TAKE              69761691
                MOVE M40090 TO KFED-MSSG-NO                       69761691
            END-IF                                                69761691
       END-IF                                                     48861503
     ELSE                                                         48861503
             MOVE ZEROS TO KFED-BASE                              48861503
     END-IF.                                                      48861503
     SKIP1                                                        48861503
     MOVE ZEROS TO KFED-AMOUNT.                                   48861503
     SKIP1                                                        48861503
     IF      KFED-GROSS-FWT3 > ZEROS                              48861503
       IF KFED-AMOUNT >= KFED-GROSS-FWT3                          48861503
             COMPUTE KFED-AMOUNT = KFED-AMOUNT - KFED-GROSS-FWT3  48861503
             MOVE ZEROS TO KFED-BASE                              48861503
       ELSE                                                       48861503
             COMPUTE KFED-BASE = KFED-GROSS-FWT3 - KFED-AMOUNT    48861503
             MOVE ZEROS TO KFED-AMOUNT                            48861503
            COMPUTE KFED-WRKPCT = KFED-FLAT-BFWT-PERCENT / 100    48861503
*****       COMPUTE KFED-DED-TO-TAKE ROUNDED = KFED-DED-TO-TAKE + 69761691
*****                                   (KFED-BASE * KFED-WRKPCT) 69761691
            COMPUTE WS-DED-TO-TAKE  ROUNDED =                     69761691
              KFED-DED-TO-TAKE + (KFED-BASE * KFED-WRKPCT)        69761691
            MOVE WS-DED-TO-TAKE TO KFED-DED-TO-TAKE               69761691
            IF WS-DED-TO-TAKE NOT = KFED-DED-TO-TAKE              69761691
                MOVE M40090 TO KFED-MSSG-NO                       69761691
            END-IF                                                69761691
       END-IF                                                     48861503
     ELSE                                                         48861503
             MOVE ZEROS TO KFED-BASE                              48861503
     END-IF.                                                      48861503
     SKIP1                                                        48861503
     MOVE ZEROS TO KFED-AMOUNT.                                   48861503
     SKIP1                                                        48861503
     IF KFED-DED-TO-TAKE <  ZERO                                  PPFEDTAX
       OR  KFED-FDTX-EXEMPT-EXMPT-FED-TX                          PPFEDTAX
       OR (KFED-FDTX-EXEMPT-W4E-ON-FILE AND                       PPFEDTAX
           NOT KFED-CT-CODE-NON-RESID-ALIEN-T)                    PPFEDTAX
           MOVE ZERO TO KFED-DED-TO-TAKE                          PPFEDTAX
     END-IF.                                                      PPFEDTAX
     SKIP2.                                                       PPFEDTAX
     GOBACK.                                                      PPFEDTAX
                                                                  69331612
*---------------------------------------------------------------  69331612
 1000-POSTDOC-FEL-NRA-DED  SECTION.                               69331612
*---------------------------------------------------------------  69331612
********E FEL GROSS IS ALREADY INCLUDED IN FWT1 GROSS. THUS, WE   69761691
********VE TO BACK IT OUT OF THE FWT1 GROSS BEFORE WE CAN DEVELOP 69761691
********SEPARATE POSTDOC FWT FEL DEDUCTION.                       69761691
**** COMPUTE WK-TOT-FWT-ADJ-GRS  =  KFED-GROSS-FWT1               69761691
****                             +  KFED-GROSS-FWT2               69761691
****                             +  KFED-GROSS-FWT3               69761691
****                             -  KFED-REDUCTIONS-FWT.          69761691
**** IF      WK-TOT-FWT-ADJ-GRS  >  KFED-POSTDOC-FEL-NRA-GRS      69761691
****     COMPUTE  KFED-GROSS-FWT1  =                              69761691
****              KFED-GROSS-FWT1  - KFED-POSTDOC-FEL-NRA-GRS     69761691
**** ELSE                                                         69761691
****     MOVE ZERO     TO KFED-GROSS-FWT1,                        69761691
****                      KFED-GROSS-FWT2,                        69761691
****                      KFED-GROSS-FWT3,                        69761691
****                      KFED-REDUCTIONS-FWT                     69761691
****     IF  WK-TOT-FWT-ADJ-GRS  >  ZERO                          69761691
****         MOVE WK-TOT-FWT-ADJ-GRS  TO KFED-POSTDOC-FEL-NRA-GRS 69761691
****     ELSE                                                     69761691
****         MOVE ZERO                TO KFED-POSTDOC-FEL-NRA-GRS 69761691
****     END-IF                                                   69761691
**** END-IF.                                                      69761691
     IF      KFED-POSTDOC-FEL-NRA-GRS  > ZERO                     69331612
*****   COMPUTE  KFED-FEL-FWT-DED-TO-TAKE  ROUNDED =              69761691
        COMPUTE  WS-DED-TO-TAKE  ROUNDED =                        69761691
                (KFED-POSTDOC-FEL-NRA-GRS  *                      69331612
                 KFED-POSTDOC-FEL-NRA-RATE)                       69331612
        MOVE WS-DED-TO-TAKE TO KFED-FEL-FWT-DED-TO-TAKE           69761691
        IF WS-DED-TO-TAKE NOT = KFED-FEL-FWT-DED-TO-TAKE          69761691
            MOVE M40090 TO KFED-MSSG-NO                           69761691
        END-IF                                                    69761691
     END-IF.                                                      69331612
     IF      KFED-FDTX-EXEMPT-EXMPT-FED-TX                        69331612
       OR   (KFED-FDTX-EXEMPT-W4E-ON-FILE AND                     69331612
             NOT KFED-CT-CODE-NONR-ALN-SPECCTRY AND               69761691
             NOT KFED-CT-CODE-NON-RESID-ALIEN-T)                  69331612
             MOVE ZEROS   TO KFED-FEL-FWT-DED-TO-TAKE             69331612
     END-IF.                                                      69331612
 1000-EXIT.      EXIT.                                            69331612
                                                                  69331612
 1500-NRA-DED-PROCESSING SECTION.                                 15611721
     SKIP1                                                        15611721
                                                                  15611721
     IF      KFED-CHECK-DATE  <  070101                           15611721
             GO TO 1500-EXIT                                      15611721
     END-IF.                                                      15611721
                                                                  15611721
     IF      KFED-GROSS-FWT1 < ZEROS                              15611721
     OR      KFED-GROSS-FWT1 = ZEROES                             15611721
             GO TO 1500-EXIT                                      15611721
     END-IF.                                                      15611721
                                                                  15611721
**** IF EMPLOYEE HAS 2020 W4 FORM, USE NEW SET OF NRA AMOUNTS     28732379
**** TO BE ADDED TO GROSS BASED ON TAX PERIOD PASSED.             28732379
     IF  KFED-W4-2020-FORM = 1                                    28732379
         IF      KFED-TAX-PERIOD  = 'M'                           28732379
                 ADD KFED-MO-PARM-AMT2     TO KFED-GROSS-FWT1     28732379
                 DISPLAY 'ADDING NRA MO AMT:' KFED-MO-PARM-AMT2   D
         ELSE                                                     28732379
            IF      KFED-TAX-PERIOD = 'S'                         28732379
                    ADD KFED-SM-PARM-AMT2  TO KFED-GROSS-FWT1     28732379
            ELSE                                                  28732379
              IF      KFED-TAX-PERIOD = 'B'                       28732379
                      ADD KFED-BI-PARM-AMT2 TO KFED-GROSS-FWT1    28732379
                 DISPLAY 'ADDING NRA BW AMT:' KFED-BI-PARM-AMT2   D
              ELSE                                                28732379
                      CONTINUE                                    28732379
              END-IF                                              28732379
            END-IF                                                28732379
         END-IF                                                   28732379
         GO TO 1500-EXIT                                          28732379
     END-IF.                                                      28732379
                                                                  28732379
     IF      KFED-TAX-PERIOD  = 'M'                               15611721
             ADD KFED-MO-PARM-AMT      TO KFED-GROSS-FWT1         15611721
     ELSE                                                         15611721
        IF      KFED-TAX-PERIOD = 'S'                             15611721
                ADD KFED-SM-PARM-AMT   TO KFED-GROSS-FWT1         15611721
        ELSE                                                      15611721
          IF      KFED-TAX-PERIOD = 'B'                           15611721
                  ADD KFED-BI-PARM-AMT TO  KFED-GROSS-FWT1        15611721
          ELSE                                                    15611721
                  CONTINUE                                        15611721
          END-IF                                                  15611721
        END-IF                                                    15611721
     END-IF.                                                      15611721
                                                                  15611721
 1500-EXIT.                                                       15611721
     EXIT.                                                        15611721
                                                                  15611721
***                                                               28732379
*** COMMENTING OUT THIS PARA SINCE IT IS OBSOLETE AND WILL        28732379
*** CAUSE COMPILE ERRORS BECAUSE OF CHANGES TO XFTA               28732379
***                                                               28732379
*2000-CALC-NRA-DEDUCTION.                                         28732379
*                                                                 28732379
**** CALCULATE ADDITIONAL FEDERAL TAX FOR NRA                     28311915
*                                                                 28732379
*    IF KFED-CT-CODE-NON-RESID-ALIEN-T OR                         28732379
*       KFED-CT-CODE-NONR-ALN-SPECCTRY                            28732379
*       COMPUTE S6 = XFTA-NO-NRALIEN (S4) + 23                    28732379
*       PERFORM VARYING S5 FROM 23 BY +1                          28732379
*         UNTIL XFTA-SALARY (S4, S5) > KFED-BASE OR (S5 = S6)     28732379
*       END-PERFORM                                               28732379
*       ADD -1                  TO S5                             28732379
*       COMPUTE WS-DED-TO-TAKE ROUNDED = WS-DED-TO-TAKE +         28732379
*                XFTA-TAX (S4, S5)     + ((KFED-BASE    -         28732379
*                XFTA-SALARY (S4, S5)) * XFTA-PCT (S4, S5))       28732379
*    END-IF.                                                      28732379
*                                                                 28732379
*2000-EXIT.                                                       28732379
*    EXIT.                                                        28732379
